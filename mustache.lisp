;;;; mustache.lisp --- Mustache Template Renderer

;;; Copyright (C) 2012  Kan-Ru Chen

;;; Author: Kan-Ru Chen <kanru@kanru.info>

;;; Permission is hereby granted, free of charge, to any person obtaining a copy of
;;; this software and associated documentation files (the "Software"), to deal in
;;; the Software without restriction, including without limitation the rights to
;;; use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies
;;; of the Software, and to permit persons to whom the Software is furnished to do
;;; so, subject to the following conditions:

;;; The above copyright notice and this permission notice shall be included in all
;;; copies or substantial portions of the Software.

;;; THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
;;; IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
;;; FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
;;; AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
;;; LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
;;; OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
;;; SOFTWARE.

;;;; Commentary:

;;; Mustache is a kind of logic-less template formats.
;;; See also: http://mustache.github.com/

;;;; TODO:
;;;
;;; * Lambda sections
;;; * Optimize compiled renderer

;;;; Code:

(in-package :mustache)

;;; Delimiter

(defparameter *default-open-delimiter* "{{")
(defparameter *default-close-delimiter* "}}")
(defparameter *default-triple-open-delimiter* "{{{")
(defparameter *default-triple-close-delimiter* "}}}")

(defvar *open-delimiter*)
(defvar *close-delimiter*)
(defvar *triple-open-delimiter* *default-triple-open-delimiter*)
(defvar *triple-close-delimiter* *default-triple-close-delimiter*)

(defun change-delimiter (text)
  (let* ((text (string-trim '(#\Space #\Tab #\=) text))
         (left-end (position #\Space text))
         (right-start (position #\Space text :from-end t))
         (right-end (position #\= text :from-end t)))
    (setf *open-delimiter* (subseq text 0 left-end))
    (setf *close-delimiter* (subseq text (1+ right-start) right-end))))

;;; Parser

(deftype space-char () '(member #\Space #\Tab))
(deftype newline-char () '(member #\Newline #\Return))
(deftype text-char () '(not (or space-char newline-char)))

(defclass token () ())
(defclass noop () ())
(defclass beginning-of-line (token) ())

(defclass text (token)
  ((text :initarg :text :accessor text)))
(defclass whitespace (text) ())
(defclass newline (text)
  ((text :initform (coerce '(#\Newline) 'string))))
(defclass crlf-newline (newline)
  ((text :initform (coerce '(#\Return #\Newline) 'string))))

(defclass tag (token)
  ((text :initarg :text :accessor text)
   (escape :initarg :escape :initform nil :accessor escapep)
   (standalone :initform nil :accessor standalone)
   (indent :initarg :indent :initform nil :accessor indent)
   (trail :initarg :trail :initform nil :accessor trail)))
(defclass can-standalone-tag (tag) ())
(defclass non-standalone-tag (tag) ())

(defclass normal-tag (non-standalone-tag) ())
(defclass implicit-iterator-tag (non-standalone-tag) ())
(defclass ampersand-tag (non-standalone-tag) ())
(defclass delimiter-tag (can-standalone-tag noop) ())
(defclass comment-tag (can-standalone-tag noop) ())
(defclass partial-tag (can-standalone-tag) ())

(defclass section-start-tag (can-standalone-tag)
  ((falsey :initarg :falsey :initform nil :accessor falsey)))
(defclass section-end-tag (can-standalone-tag)
  ((falsey :initarg :falsey :initform nil :accessor falsey)))
(defclass section-tag (can-standalone-tag)
  ((tokens :initarg :tokens :accessor tokens)
   (falsey :initarg :falsey :initform nil :accessor falsey)))

(defun make-tag (&key text escape)
  (let ((funchar (char text 0))
        (tag-text (string-trim '(#\Space #\Tab) (subseq text 1)))
        (text (string-trim '(#\Space #\Tab) text)))
   (case funchar
     (#\& (make-instance 'ampersand-tag :text tag-text))
     (#\# (make-instance 'section-start-tag :text tag-text))
     (#\^ (make-instance 'section-start-tag :text tag-text :falsey t))
     (#\/ (make-instance 'section-end-tag :text tag-text))
     (#\! (make-instance 'comment-tag :text ""))
     (#\= (prog1
              (make-instance 'delimiter-tag :text tag-text)
            (change-delimiter tag-text)))
     (#\> (make-instance 'partial-tag :text tag-text))
     (#\. (make-instance 'implicit-iterator-tag :text tag-text))
     (t (make-instance 'normal-tag :text text :escape escape)))))

(defmethod print-object ((object tag) stream)
  (print-unreadable-object (object stream :type t :identity t)
    (princ (text object) stream)))

;;; Lexer

;; Invariant token
(defvar beginning-of-line (make-instance 'beginning-of-line))
(defvar newline (make-instance 'newline))
(defvar crlf-newline (make-instance 'crlf-newline))

(defun string-match (pattern string &optional (start 0))
  (let ((end2 (+ start (length pattern)))
        (len (length string)))
    (and (>= len end2)
         (string= pattern string :start2 start :end2 end2))))

(defun text-char-p (char)
  (typep char 'text-char))

(defun space-char-p (char)
  (typep char 'space-char))

(defun newline-char-p (char)
  (typep char 'newline-char))

(defvar crlf (coerce '(#\Return #\Newline) 'string))

(defun read-text (string &optional (start 0) (end (length string)))
  (loop for idx from start below end
        for char = (char string idx)
        while (text-char-p char)
        until (string-match *open-delimiter* string idx)
        collect char into text
        finally (return (values (make-instance 'text :text (coerce text 'string))
                                idx))))

(defun read-whitespace (string &optional (start 0) (end (length string)))
  (loop for idx from start below end
        for char = (char string idx)
        while (space-char-p char)
        until (string-match *open-delimiter* string idx)
        collect char into text
        finally (return (values (make-instance 'whitespace :text (coerce text 'string))
                                idx))))

(defun read-newline (string &optional (start 0) (end (length string)))
  (declare (ignore end))
  (cond
    ((string-match crlf string start)
     (values crlf-newline
             (+ 2 start)))
    ((newline-char-p (char string start))
     (values newline
             (1+ start)))))

(defun read-tag (string &optional triple (start 0) (end (length string)))
  (let ((tag-open (if triple
                  *triple-open-delimiter*
                  *open-delimiter*))
        (tag-close (if triple
                   *triple-close-delimiter*
                   *close-delimiter*)))
    (when (string-match tag-open string start)
      (incf start (length tag-open))
      (loop for idx from start below end
            for char = (char string idx)
            when (string-match tag-close string idx)
              do (progn
                   (incf idx (length tag-close))
                   (loop-finish))
            collect char into text
            finally (return (values (make-tag :text (coerce text 'string)
                                              :escape (not triple))
                                    idx))))))

(defun read-token (string &optional (start 0) (end (length string)))
  (let ((char (char string start)))
    (cond
      ((space-char-p char)
       (read-whitespace string start end))
      ((newline-char-p char)
       (read-newline string start end))
      ((string-match *triple-open-delimiter* string start)
       (read-tag string t start end))
      ((string-match *open-delimiter* string start)
       (read-tag string nil start end))
      (t
       (read-text string start end)))))

(defun scan (string &optional (start 0) (end (length string)))
  (let ((idx start)
        (*open-delimiter* *default-open-delimiter*)
        (*close-delimiter* *default-close-delimiter*))
    (loop while (> end idx)
          with token
          when (zerop idx)
            collect beginning-of-line
          do (multiple-value-setq (token idx)
               (read-token string idx))
          collect token
          when (and (< idx end)
                    (typep token 'newline))
            collect beginning-of-line)))

;;; Parser

(deftype text-token ()
  '(not (or beginning-of-line can-standalone-tag newline whitespace)))

(defun newlinep (token)
  (typep token 'newline))

(defun tagp (token)
  (typep token 'tag))

(defun collect-line (tokens)
  (loop for start = 0 then (1+ finish)
        for finish = (position-if #'newlinep tokens :start start)
        when (subseq tokens start (and finish (1+ finish)))
        collect it
        until (null finish)))

(defun standalone-p (tokens)
  (when (eq (car tokens) beginning-of-line)
    (loop for token in tokens
          count (typep token 'can-standalone-tag) into tags
          count (typep token 'text-token) into texts
          finally (return (and (= 1 tags)
                               (= 0 texts))))))

(defun make-standalone-tag (tokens)
  (let* ((pos (position-if #'tagp tokens))
         (tag (elt tokens pos)))
    (setf (indent tag) (subseq tokens 0 pos))
    (setf (trail tag) (subseq tokens (1+ pos)))
    (setf (standalone tag) t)
    tag))

(defun trim-standalone (tokens)
  (loop for line in (collect-line tokens)
        append (if (standalone-p line)
                   (list (make-standalone-tag line))
                   line)))

(defun tag-match (tag1 tag2)
  (string-equal (text tag1) (text tag2)))

(defun make-section-tag (tag tokens)
  (make-instance 'section-tag
                 :tokens tokens
                 :text (text tag)
                 :falsey (falsey tag)))

(defun push-group (acc)
  (cons nil acc))

(defun push-token (token acc)
  (cons (cons token (car acc)) (cdr acc)))

(defun pop-group (acc)
  (cdr acc))

(defun top-group (acc)
  (reverse (car acc)))

(defun push-section-tag (tag acc)
  (push-token (make-section-tag tag (top-group acc))
              (pop-group acc)))

(defun group-sections (tokens &optional end-tags acc)
  (if (not tokens)
      (top-group acc)
      (let ((token (car tokens))
            (rest (cdr tokens))
            (end-tag (car end-tags)))
        (typecase token
          (section-start-tag
           (group-sections rest (cons token end-tags) (push-group acc)))
          (section-end-tag
           (when (tag-match token end-tag)
             (group-sections rest (cdr end-tags)
                             (push-section-tag end-tag acc))))
          (otherwise
           (group-sections rest end-tags (push-token token acc)))))))

(defun textp (token)
  (typep token 'text))

(defun fold-text (tokens)
  (loop for start = 0 then next
        for finish = (position-if (complement #'textp) tokens :start start)
        for next = (and finish (position-if #'textp tokens :start finish))
        for texts = (subseq tokens start finish)
        when texts
          collect (make-instance 'text :text
                                 (format nil "狺磲疸狎＇翦翦趔┅麒孱ㄡ钿骈铋箬篚怏羼麸脲铙骈铋箬铄舂狃疱钿轸麒殪铄舂ㄤ彐躅疳蝮翦眇灬翦ㄧ蝻躔箦泗轱铙ㄦ镬洵翦趄轫篝犷溽祜铄筱犷翦眇灬翦┅┅换蔑铘屮ㄤ彐沆狍泔铘屮īè溽翎洪铋翎蜱轰狒洪铋翩矧铋横沣弩箫溽翎ㄩ钿孱洪铋翎蜱洪钿孱洪铋翩矧铋横沣弩箫轭溴铘疳螋獒祗洪铋翎蜱吼狎糸犰洪铋翩矧铋横沣弩箫疳螋獒祗铄洪铋翎蜱侯屮洪铋翩矧铋横沣弩箫铄舂┅ㄤ彐躅疳蝮瀛脲篝蜷铉祜镳骘篝狎翳孱ū骈铋箬骘骈铋箬痫箝糸镱＼篝蜷铉后翎螋篝狎舂泔祆邈ㄩ铘弪篝蜷铉躔汜箦篚怏羼篝蜷铉篝狎骈铋箬┅弘妁黠蜾躅糸铛祆骈铋箬┅ㄤ彐躅筢鲥栳箬翎忪箫躜沐豉疱汜箦箫躜沐篝蜷铉箫躜沐铛祆铋飑鲥泗矧ㄡ钿痨躞戾铉翳箫躜沐┅磲鲥泗矧＇筢鲥栳箬翎忪箫躜沐┅扉篝戾è翎忪磲脲栳箬翎忪濠┅ㄤ镬轶ㄣ镱箫躜沐箦翩ㄧ弭栳箬ㄣ狎泔铙翎忪濠筢鲥栳箬翎忪ㄣ潋泔铙┅┅翎忪濠雉桢蝼轶箫躜沐┅ㄤ彐躅眭篝徙桢泔铘屮é脲溽翎疳螋獒祗⒚蝈狒眭篝徙桢泔铘屮骝镯犰轶牧粤磲脲轭篝犷沐с镱翦轰狒筢鲥栳箬翎忪溽翎吼狎糸犰筢鲥栳箬翎忪疳螋獒祗┅ㄤ彐躅磲脲泔铘屮é镳糸镱犰溽翎泔铘屮舂戾è泗磲脲轭篝犷沐с镱翦舂┅ㄩ泔铘屮痱镧箦翩ㄤ狒泗溽翎箦翩ㄩ钿孱泗ㄩ钿孱泔铘屮舂箦翩疳螋獒祗泗疳螋獒祗泔铘屮舂箦翩铄泗泔铘屮舂┅泗┅ㄤ彐珏铄蜷泔铘屮舡珏脲泔铘屮舂ê滹沲礤铘狒轱⑶弭溽翎骝镯孟卧咆怡伺佼┅ㄤ彐礤翳镤泔铘屮舡珏è脲篝蜷铉泔铘屮舂ㄣ镱翦舡珏疳蝮瀛脲脲泔铘屮舂ㄤ彐礤翳镤泔铘屮舡珏è脲扉篝泔铘屮舂眭祠轲戾鲠祯瀛忾钿ㄤ狒骈钿ㄣ镱翦舡珏ㄣ狎脲泔铘屮舂ㄩㄣ潋脲ㄣ镱翦舡珏ㄣ潋脲溽翎鲠祯弩溽翎骈钿┅┅ㄤ彐礤翳镤泔铘屮舡珏è脲簌礅镬ㄣ镱翦栳箬翎忪濠ㄧ弭栳箬脲泔铘屮舂ㄤ彐礤翳镤泔铘屮舡珏è脲簌礅镬ㄣ镱翦泔铘屮舂眭祠轲戾鲠祯瀛忾钿ㄤ狒骈钿ㄣ镱翦舡珏脲ㄤ狒泔铘屮舂ㄩ骈钿鲠祯弩溽翎骈钿麒孱铄泔铘屮舂ㄣ镱翦舡珏脲铄泔铘屮舂┅┅ㄤ彐礤翳镤泔铘屮舡珏脲ㄣ镱翦铛祆┅ㄤ邈灬蝈ㄩ珙矧脲┅鲠祯弩┅ㄤ彐礤翳镤箦翩泔铘屮舡珏舂鲠祯脲泔铘屮舂箦翩ㄧ弭栳箬脲ㄤ狒泔铘屮舂鲠祯濠换嗅螋獒祗ㄤ彐躅蝈徜疳螋獒ㄦ殪孱犴镳糸镱犰泔铘屮舂鏖翳镳孱骈戾篝蝈犴骈戾钺礤洪姝滹弩铒舡屮轶铋飑ㄩ篝蝈犴戾è怩骀弪磲脲篝蜷铉ㄦ殪瀛戾铉翳篝蝈犴┅┅蝈徜箦聃孱沐怩骀弪篝蝈犴┅ㄣ镱翦舡珏骈戾钺礤疳螋獒祗泔铘屮舂┅┅换义钿弪轭蒸殪ㄤ彐疳蜥礤翦汨狎麸弩汜疱螵⒓睛堍Бㄤ彐躅弩汜疱汨狎ㄣ栳颟ㄣ狍汨狎ǎ堞犴鸹ǎ芗祠虎ǎ芫玺虎ǎ堙狃矬虎ǎ堍聃雉虎ㄦ矧磲铋Γ浠ㄣ栳颦泔溴汨狎┅┅ㄤ彐躅弩汜疱篝蜷铉ㄦ戾è铄邃蟓弩汜疱ㄣ栳颟ㄦ轭汨狎汨狎麸弩汜疱螵┅鏖翳秕麴豸麸篝蜷铉秕舂祜镳骘篝狎翳孱ū痫螬骘痫痫箝糸镱殒＇铄邃蟓弩汜疱篝蜷铉后翎螋篝狎舂滹黩轸瀛箦聃孱沐篝蜷铉秕后翎螋篝狎哄钿痫螬麒孱痫滹黩轸瀛箦聃孱沐ㄥ筱狃瀛汨狎ㄣ栳篝蜷铉痫螬秕舂麒殪痫螬┅ㄤ彐礤翳镤弩汜疱è镡赍泗犴疱蝮犷洵翎绌铋飑ㄤ彐礤翳镤箦翩弩汜疱皓铄鳝鲠祯镡赍泗犴疱蝮犷洵翎绌铋飑ㄤ彐珏铄蜷痱轭舡溽翎ㄤ狒弩汜疱镳糸镱犰泔铘屮舂ㄤ彐礤翳镤痱轭舡溽翎è溽翎篝蜷铉弩汜疱镳糸镱犰泔铘屮舂ㄤ邈灬蝈ㄩ珙矧泔铘屮舂ㄩ弩汜疱痱轭ㄥ筱狃溽翎┅痱轭溽翎┅ㄤ彐礤翳镤痱轭舡溽翎è溽翎鲥泗矧弩汜疱镳糸镱犰泔铘屮舂ㄤ邈灬蝈ㄩ珙矧弩汜疱皓祜镳骘麸脲徙蝻篌溽翎滹痱轭舡麸脲麸脲泔铘屮舂┅ㄤ彐礤翳镤痱轭舡溽翎è溽翎骢钽糸镱弩汜疱镳糸镱犰泔铘屮舂戾舄è鲠祯ㄦ矧磲铋幄ㄦ躅汜祆溽翎┅ㄦ躅眭篝徙桢泔眇殪鲠祯濠秕麴豸鏖翳秕麴豸麸篝蜷铉í篝犷溽蜾秕麴豸ㄦ躅汜祆骢泔铘屮舂┅ㄩ弩汜疱痱轭ㄥ筱狃秕麴豸┅痱轭秕麴豸┅┅ㄤ彐礤翳镤痱轭舡溽翎麸脲弩汜疱镳糸镱犰泔铘屮舂ㄤ邈灬蝈ㄩ珙矧弩汜疱泔铘屮舂痱轭麸脲瞟ㄤ彐躅痱轭舡轭溴铘é镳糸镱犰泔铘屮舂麒孱ㄡ钿泔铘屮ㄩ钿孱泔铘屮舂ㄦ躅汜祆ㄣ狎ㄩ钿孱泔铘屮舂铋飑┅换蔑眇殪弪ㄤ彐珏铄蜷屙轸麸脲麸脲瞟ㄤ彐礤翳镤屙轸麸脲è麸脲翦舂啜痱轭翦麸脲瞟┅ㄤ彐礤翳镤屙轸麸脲è麸脲翎绌啜眭祠轲戾鲠祯瀛忾钿ㄤ狒骈钿ㄣ镱翦舡珏翦麸脲瞟泔铘屮舂麒孱骈钿痱轭舡溽翎溽ㄥ筱狃屦麸脲瞟泔铘屮舂┅ㄤ彐礤翳镤屙轸麸脲è麸脲疳螋獒飙翎绌啜戾è骢眭篝徙桢泔眇殪矧蝈徜疳螋獒翦麸脲瞟泔铘屮舂┅┅瘐箬灬礅溽é镳糸镱犰泔铘屮舂ㄤ邈灬蝈ㄩ珙矧徕戾泔铘屮舂括屙轸麸脲铙ㄩ钿孱麸脲瞟┅ㄩ钿孱泔铘屮舂ㄦ躅汜祆骢泔铘屮舂痫ㄩ钿孱泔铘屮舂┅ㄤ彐礤翳镤屙轸麸脲è麸脲箦泗轱瞽翎绌啜眭祠轲戾鲠祯瀛忾钿ㄣ豇骈钿ㄣ镱翦舡珏翦麸脲瞟泔铘屮舂麒孱矧骈钿ㄦ犰箦麸脲瞟ㄦ戾è骢é镳糸镱犰泔铘屮舂ㄤ邈灬蝈ㄩ珙矧徕戾泔铘屮舂括屙轸麸脲铙麸脲铙麸脲瞟┅ㄩㄦ犰箦麸脲瞟啜麒孱铛祆泗ㄦ躅泔铘屮舂啜豉疱汜箦泗ㄨ狍璀翎忪ㄦ躅磲脲泔铘屮泗泔铘屮舂┅铛祆鲥泗矧祜镳骘泗徙蝻篌泗滹ㄦ躅磲脲泔铘屮泗泔铘屮舂┅ㄦ躅泔铘屮舂┅┅┅ㄤ彐礤翳镤屙轸麸脲è麸脲轫痨殂轸轸弪狒矧翎绌啜痱轭舡溽翎ㄤ狒泔铘屮舂ㄥ筱狃屦麸脲瞟泔铘屮舂ㄤ彐礤翳镤屙轸麸脲è麸脲忮玳铑轭绛镦扉铄┅ㄤ邈灬蝈ㄩ珙矧麸脲瞟啜痱轭舡轭溴铘泔铘屮舂换铒镳麸脲铙ㄤ彐礤翳镤屙轸麸脲è麸脲铒镳┅啜鲠祯弩┅ㄤ彐躅屙轸麸脲铙麸脲铙祜镳骘麸脲轭麸脲铙泔祆邈ㄥ黹舡麸脲麸脲瞟┅ㄤ彐躅屙轸怙澌麸脲铙啜戾è泔铘屮ㄩ扉篝泔铘屮舂眭篝徙桢泔铘屮轰狒泔铘屮舂泔铘屮舂┅ㄤ邈灬蝈ㄩ珙矧徕戾泔铘屮舂括屙轸麸脲铙麸脲铙┅┅换深翦蜴徙弩ㄤ彐躅眭篝徙桢鲥蝮轱ī⑼躞翎汨霰阿ㄤ彐珏铄蜷眭篝徙桢泔眇殪翦眇灬翦ê滹沲礤铘狒轱⒁弭躜泔眇殪邃蝈钿弪轭骢钽糸镱┅ㄤ彐礤翳镤眭篝徙桢泔眇殪è翦眇灬翦篝蜷铉┅ㄣ镯痖戾铋啜灬礅溽é镳糸镱犰泔铘屮舂括屙轸怙澌疳蝮翦眇灬翦┅┅ㄤ彐珏铄蜷眭篝徙桢蝈钿弪翦眇灬翦镳糸镱犰泔铘屮舂ê滹沲礤铘狒轱⒁孱溴耘托塘耘鏖翳镳糸镱犰孟卧咆麸篝蜷铉┅ㄤ彐礤翳镤眭篝徙桢蝈钿弪è翦眇灬翦篝蜷铉镳糸镱犰泔铘屮舂戾è骢眭篝徙桢泔眇殪翦眇灬翦┅鏖翳秕麴豸麸篝蜷铉í篝犷溽蜾秕麴豸ㄦ躅汜祆骢泔铘屮舂┅ㄤ彐磲泸溴骓躞翎汨钺礤翦眇灬翦⒛彐轭钺礤蝈钿弪弪镦篝蜷铉耘托塘耘啜溴骢钺礤é镳糸镱犰泔铘屮舂括屙轸怙澌疳蝮翦眇灬翦┅┅换眭篝徙桢扉箴孱潴桢蝈换田汜轴蜷徕戾蠛换盹溴扉箴换蓬浜